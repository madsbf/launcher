apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

allprojects {
    afterEvaluate { project ->
        def propsFile = rootProject.file('../security/keystore.properties')
        def configName = 'release'

        if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            android.signingConfigs[configName].storeFile = file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "me.madsbf.launcher"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 2
        versionName "1.0.0-alpha1"
    }

    signingConfigs {
        release {
            // Hack: These values are overwritten, but are needed in order to get an aligned APK.
            storeFile file("/path/to/keystore")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "DEBUG_MODE", "false"
        }

        debug {
            buildConfigField "Boolean", "DEBUG_MODE", "true"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:palette-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile "com.android.support:support-v4:23.0.1"
    compile 'dk.shape.library:SHPCollections:2.0.0-rc2'
    compile 'dk.shape.library:allanaction:1.0.0-alpha5'
    compile 'org.jetbrains.kotlin:kotlin-stdlib:0.12.1230'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
}
